group 'net.dankito.filechooserdialog'
version VersionName
def mavenArtifactId = "filechooserdialog-android"


buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        // Nexus staging plugin has to be downgraded to 0.10.0 to be applicable to sub projects, see https://github.com/UweTrottmann/SeriesGuide/commit/ca33e8ad2fa6cc5c426450c8aef3417ba073ca7f
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.10.0"
    }
}


apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'maven'
apply plugin: 'signing'

// So after executing uploadArchives staged repository can be closed and released by executing FileChooserDialog:closeAndReleaseRepository
apply plugin: 'io.codearte.nexus-staging'



android {
    compileSdkVersion CompileSdkVersion

    defaultConfig {
        minSdkVersion MinSdkVersion
        targetSdkVersion TargetSdkVersion

        versionCode VersionCode
        versionName VersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations {
        javadocDeps
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "net.dankito.utils:java-utils:$javaUtilsVersion"

    compile "net.dankito.utils:android-utils:$androidUtilsVersion", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }

    implementation "net.dankito.mime:mime-utils:$mimeUtilsVersion", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7:'
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'org.jsoup', module: 'jsoup'
    }

    implementation "org.slf4j:slf4j-android:1.7.25"

    implementation "com.android.support:appcompat-v7:$appCompatVersion"
    implementation "com.android.support:design:$appCompatVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "com.android.support.test:runner:$androidTestRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}


/*          publish to maven central            */

def isReleaseVersion = !version.endsWith("SNAPSHOT")
def areSigningPropertiesSet = isPropertySet('signing.secretKeyRingFile') && isPropertySet('signing.keyId') && isPropertySet('signing.password')
def areOssrhPropertiesSet = isPropertySet('ossrhUsername') && isPropertySet('ossrhPassword')

def isPropertySet(propertyName) {
    return properties[propertyName] != null &&  ! (properties[propertyName] as String).isEmpty()
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    required { isReleaseVersion && areSigningPropertiesSet && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository url: repositories.mavenLocal().url

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // set ossrhUsername and ossrhPassword in your gradle.properties (in ~/.gradle/gradle.properties or project's gradle.properties)
            if (areOssrhPropertiesSet) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name mavenArtifactId
                artifactId = mavenArtifactId
                packaging 'jar'

                description "Material Design File Chooser Dialog for Android."
                url 'https://github.com/dankito/AndroidFileChooserDialog'

                scm {
                    connection 'scm:git:git://github.com/dankito/AndroidFileChooserDialog.git'
                    developerConnection 'scm:git:git@github.com:dankito/AndroidFileChooserDialog.git'
                    url 'https://github.com/dankito/AndroidFileChooserDialog'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dankito'
                        name 'Christian Dankl'
                        email 'maven@dankito.net'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "net.dankito"
}
